
&Пластилин Перем СоединенияСерверныхСобытий;
&Пластилин Перем ТопикиСерверныхСобытий;

&Желудь
Процедура ПриСозданииОбъекта()

КонецПроцедуры

Функция КоличествоСоединений(Топик) Экспорт
	Возврат СоединенияСерверныхСобытий.Количество(Топик);
КонецФункции

Процедура ОтправитьСообщениеВсем(Топик, Сообщение) Экспорт
	ПроверитьТопик(Топик);

	Соединения = СоединенияСерверныхСобытий.ПолучитьСоединенияПоТопику(Топик);

	ОтправитьСообщениеКлиентам(Соединения, Сообщение);

КонецПроцедуры

Процедура ОтправитьСообщениеПоИдСоединения(ИдСоединения, Сообщение) Экспорт

	Соединения = СоединенияСерверныхСобытий.ПолучитьСоединениеПоИдСоединения(ИдСоединения);

	ОтправитьСообщениеКлиентам(Соединения, Сообщение);

КонецПроцедуры

Процедура ОтправитьСообщениеСписку(Топик, Сообщение, СписокИдентификаторов) Экспорт
	ПроверитьТопик(Топик);

	Соединения = СоединенияСерверныхСобытий.ПолучитьСоединенияПоТопикуСпискуИдентификаторов(Топик, СписокИдентификаторов);

	ОтправитьСообщениеКлиентам(Соединения, Сообщение);

КонецПроцедуры

Процедура ОтправитьСообщениеВсемКроме(Топик, Сообщение, СписокИсключенийИдентификаторов) Экспорт
	ПроверитьТопик(Топик);

	Соединения = СоединенияСерверныхСобытий.ПолучитьСоединенияПоТопикуКромеСпискаИдентификаторов(Топик, СписокИсключенийИдентификаторов);

	ОтправитьСообщениеКлиентам(Соединения, Сообщение);

КонецПроцедуры

Процедура ОтправитьСообщение(Идентификатор, Топик, Сообщение) Экспорт
	ПроверитьТопик(Топик);

	Соединения = СоединенияСерверныхСобытий.ПолучитьСоединениеПоИдентификатору(Топик, Идентификатор);

	ОтправитьСообщениеКлиентам(Соединения, Сообщение);

КонецПроцедуры

Процедура ОтправитьСообщениеКлиентам(Соединения, Сообщение)

	ДДСобщения = ПолучитьДвоичныеДанныеИзСтроки(Сообщение.Сформировать());

	Для Каждого Соединение из Соединения Цикл
		ОтправитьВПопытке(Соединение.Соединение, ДДСобщения);
	КонецЦикла;
	
КонецПроцедуры

Функция ОтправитьВПопытке(Соединение, ДвоичныеДанныеОтвета) Экспорт
	Попытка
		Соединение.ОтправитьДвоичныеДанные(ДвоичныеДанныеОтвета);	
		Возврат Истина;
	Исключение
		Возврат Ложь;	
	КонецПопытки;
КонецФункции

Процедура ПроверитьТопик(Топик)
	Если Не ТопикиСерверныхСобытий.Существует(Топик) Тогда
		ВызватьИсключение СтрШаблон("Топик %1 не зарегистрирован", Топик);
	КонецЕсли;
КонецПроцедуры

