&Пластилин Перем Настройки;
&Пластилин Перем ПарсерЗапросов;

&Желудь
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

Процедура Обработать(Соединение) Экспорт

	Попытка
		ДанныеЗапроса = ПрочитатьДвоичныеДанныеИзСоединения(Соединение);
	Исключение
		ЗакрытьСоединениеВПопытке(Соединение);
		// TODO: ДОБАВИТЬ ЛОГГЕР
		Сообщить("Не удалось обработать входящее соединение");
		Возврат;
	КонецПопытки;

	ДвоичныеДанныеОтвета = ПарсерЗапросов.ПолучитьОтветДвоичнымиДанными(ДанныеЗапроса);

	ОтправитьВПопытке(Соединение, ДвоичныеДанныеОтвета);

	ЗакрытьСоединениеВПопытке(Соединение);

КонецПроцедуры

Функция ПрочитатьДвоичныеДанныеИзСоединения(Соединение)
	Если Настройки.ЗадержкаПередЧтениемСокета > 0 Тогда
		Приостановить(Настройки.ЗадержкаПередЧтениемСокета);
	КонецЕсли;

	ДанныеЗапроса = Соединение.ПрочитатьДвоичныеДанные();
	
	Возврат ДанныеЗапроса;
КонецФункции

Процедура ОтправитьВПопытке(Соединение, ДвоичныеДанныеОтвета)
	Попытка
		Соединение.ОтправитьДвоичныеДанные(ДвоичныеДанныеОтвета);	
	Исключение
		// TODO: ДОБАВИТЬ ЛОГГЕР
		Сообщить("Не удалось отправить ответ");	
	КонецПопытки;
КонецПроцедуры

Процедура ЗакрытьСоединениеВПопытке(Соединение)
	Попытка
		// Некоторые клиенты ломаются, когда после отправки
		// в сокет данных сразу закрываемся. Ждем.
		Если Настройки.ЗадержкаПередЗакрытиемСокета > 0 Тогда
			Приостановить(Настройки.ЗадержкаПередЗакрытиемСокета);
		КонецЕсли;
		Соединение.Закрыть();	
	Исключение
		// TODO: ДОБАВИТЬ ЛОГГЕР
		Сообщить("Не удалось закрыть входящее соединение");	
	КонецПопытки;
КонецПроцедуры