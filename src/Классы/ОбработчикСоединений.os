&Пластилин Перем Настройки;
&Пластилин Перем МенеджерВходящихСоединений;

Перем ТаймаутЧтения;

&ЛогВебСервера 
Перем Лог;

&Желудь
Процедура ПриСозданииОбъекта()
	ТаймаутЧтения = 10;
КонецПроцедуры

Процедура Обработать(Соединение) Экспорт

	Попытка
		ДанныеЗапроса = ПрочитатьДвоичныеДанныеИзСоединения(Соединение);
	Исключение
		ЗакрытьСоединениеВПопытке(Соединение);
		ТекстОшибки = ОписаниеОшибки();
		Лог.Ошибка("Не удалось обработать входящее соединение: " + ТекстОшибки);
		Возврат;
	КонецПопытки;

	Результат = МенеджерВходящихСоединений.ПринятьСоединение(Соединение, ДанныеЗапроса);

	ОтправитьВПопытке(Соединение, Результат.Ответ);

	Если Результат.ЗакрыватьСоединение Тогда

		ЗакрытьСоединениеВПопытке(Соединение);

	КонецЕсли;

КонецПроцедуры

Функция ПрочитатьДвоичныеДанныеИзСоединения(Соединение)
	Если Настройки.ЗадержкаПередЧтениемСокета > 0 Тогда
		Приостановить(Настройки.ЗадержкаПередЧтениемСокета);
	КонецЕсли;

	ДанныеЗапроса = ВычитатьВПопыткеДвоичныеДанные(Соединение);
	
	Возврат ДанныеЗапроса;
КонецФункции

Функция ВычитатьВПопыткеДвоичныеДанные(Соединение)

	Данные = Новый Массив();

	Попытка
		Пока Истина Цикл
			Соединение.ТаймаутЧтения = ТаймаутЧтения;
			ДанныеСоединения = Соединение.ПрочитатьДвоичныеДанные(Настройки.РазмерБуфера);
			Данные.Добавить(ДанныеСоединения);
		КонецЦикла;
	Исключение
	
	КонецПопытки;

	Возврат СоединитьДвоичныеДанные(Данные);

КонецФункции

Процедура ОтправитьВПопытке(Соединение, ДвоичныеДанныеОтвета) Экспорт
	Попытка
		Соединение.ОтправитьДвоичныеДанные(ДвоичныеДанныеОтвета);	
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		Лог.Ошибка("Не удалось отправить ответ: " + ТекстОшибки);	
	КонецПопытки;
КонецПроцедуры

Процедура ЗакрытьСоединениеВПопытке(Соединение) Экспорт
	Попытка
		// Некоторые клиенты ломаются, когда после отправки
		// в сокет данных сразу закрываемся. Ждем.
		Если Настройки.ЗадержкаПередЗакрытиемСокета > 0 Тогда
			Приостановить(Настройки.ЗадержкаПередЗакрытиемСокета);
		КонецЕсли;
		Соединение.Закрыть();	
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		Лог.Ошибка("Не удалось закрыть входящее соединение: " + ТекстОшибки);		
	КонецПопытки;
КонецПроцедуры