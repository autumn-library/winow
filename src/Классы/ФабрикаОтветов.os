Перем Поделка;
Перем МенеджерОтображений;

&Пластилин Перем ШифрованиеВебСокета;
&Пластилин Перем МенеджерСессий;

&Желудь
Процедура ПриСозданииОбъекта(
							&Пластилин("Поделка") _Поделка,
							&Пластилин("МенеджерОтображений") _МенеджерОтображений
							)
	Поделка = _Поделка;
	МенеджерОтображений = _МенеджерОтображений;
КонецПроцедуры

Функция СерверноеСобытие() Экспорт
	Возврат Новый СерверноеСобытие(); 
КонецФункции

Функция ПустойОтвет() Экспорт
	Возврат Поделка.НайтиЖелудь("Ответ");	
КонецФункции

Функция НовыйОтвет(КодОшибки = Неопределено) Экспорт
	НовыйОтвет = ПустойОтвет();
	Если ЗначениеЗаполнено(КодОшибки) Тогда
		НовыйОтвет.УстановитьТекстШаблона(ШаблонОшибки(КодОшибки));
	КонецЕсли;
	ЗаполнитьОтветПредопределеннымиЗначениям(НовыйОтвет);
	Возврат НовыйОтвет;
КонецФункции

Функция ОтветРукопожатияВебСокета(Запрос) Экспорт
	Ответ = ПустойОтвет();

	Ответ.СостояниеКод = 101;
	Ответ.СостояниеТекст = "Switching Protocol";

	Ответ.Заголовки["Sec-WebSocket-Accept"] = ШифрованиеВебСокета.ХешОтветаРукопожатия(Запрос.КлючРукопожатия);
	Ответ.Заголовки["Upgrade"] = "websocket";
	Ответ.Заголовки["Connection"] = "Upgrade";
	Ответ.Заголовки[МенеджерСессий.ИмяКукаСессии()] = Запрос.Сессия.Идентификатор();

	Возврат Ответ;
КонецФункции

Функция НовыйОтвет404() Экспорт
	НовыйОтвет = НовыйОтвет(404);
	НовыйОтвет.УстановитьСостояниеНеНайдено();
	Возврат НовыйОтвет;
КонецФункции

Функция НовыйОтвет500() Экспорт
	НовыйОтвет = НовыйОтвет(500);
	НовыйОтвет.УстановитьСостояниеОшибка();
	Возврат НовыйОтвет;
КонецФункции

Функция НовыйОтвет401() Экспорт
	НовыйОтвет = НовыйОтвет(401);
	НовыйОтвет.УстановитьСостояниеНеАвторизован();
	Возврат НовыйОтвет;
КонецФункции

Функция НовыйОтвет403() Экспорт
	НовыйОтвет = НовыйОтвет(403);
	НовыйОтвет.УстановитьСостояниеДоступЗакрыт();
	Возврат НовыйОтвет;
КонецФункции

Процедура ЗаполнитьОтветПредопределеннымиЗначениям(Ответ)
	Ответ.УстановитьСостояниеОК();
	Ответ.Заголовки["Server"] = "WINOW";
	Ответ.УстановитьТипКонтента("html");
КонецПроцедуры

Функция ШаблонОшибки(КодОшибки)
	Возврат МенеджерОтображений.ПолучитьОтображение(КодОшибки);
КонецФункции
