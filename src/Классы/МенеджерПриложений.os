#Использовать autumn

Перем Приложения;
Перем КонтекстСервера;

&Желудь
Процедура ПриСозданииОбъекта(
							&Пластилин("КонтекстПриложения") _КонтекстСервера
							)
	Приложения = Новый Соответствие();
	КонтекстСервера = _КонтекстСервера;
КонецПроцедуры

Функция ДобавитьКонтекстПриложения(ИмяПриложения) Экспорт

	Если НЕ Приложения.Получить(ИмяПриложения) = Неопределено Тогда
		ВызватьИсключение "Приложение с таким имененм уже добавлено: " +  ИмяПриложения;
	КонецЕсли;

	КонтекстПриложения = Новый КонтекстПриложения();

	ДобавитьНапильникМаршрутизатор(КонтекстПриложения);

	ДобавитьОбщиеДанные(КонтекстПриложения);

	Приложения.Вставить(ИмяПриложения, КонтекстПриложения);

	Возврат КонтекстПриложения;
	
КонецФункции

Процедура ДобавитьОбщиеДанные(КонтекстПриложения)

	КонтекстПриложения.ЗарегистрироватьЖелудь(Тип("ОбщийКонтейнер"));

	ОбщийКонтейнер = КонтекстПриложения.ПолучитьЖелудь("ОбщийКонтейнер");
	ОбщийКонтейнер.Настройки = КонтекстСервера.ПолучитьЖелудь("Настройки");
	ОбщийКонтейнер.Перечисления = КонтекстСервера.ПолучитьЖелудь("Перечисления");
	ОбщийКонтейнер.Маршрутизатор = КонтекстСервера.ПолучитьЖелудь("Маршрутизатор");
	ОбщийКонтейнер.ФабрикаОтветов = КонтекстСервера.ПолучитьЖелудь("ФабрикаОтветов");
	ОбщийКонтейнер.Парсеры = КонтекстСервера.ПолучитьЖелудь("Парсеры");
	ОбщийКонтейнер.МенеджерДоступа = КонтекстСервера.ПолучитьЖелудь("МенеджерДоступа");

КонецПроцедуры

Процедура ДобавитьНапильникМаршрутизатор(КонтекстПриложения)

	КонтекстПриложения.ЗарегистрироватьНапильник(Тип("СборщикМаршрутов"));

	СборщикМаршрутов = КонтекстПриложения.ПолучитьЖелудь("СборщикМаршрутов");
	СборщикМаршрутов.УстановитьМаршрутизатор(КонтекстСервера.ПолучитьЖелудь("Маршрутизатор"));	
	СборщикМаршрутов.УстановитьМенеджерОтображений(КонтекстСервера.ПолучитьЖелудь("МенеджерОтображений"));
	СборщикМаршрутов.УстановитьМенеджерДоступа(КонтекстСервера.ПолучитьЖелудь("МенеджерДоступа"));

КонецПроцедуры

Функция ПолучитьКонтекстПриложенияПоИмени(ИмяПриложения) Экспорт

	КонтекстПриложения = КонтекстПриложенияПоИмени(ИмяПриложения);

	Если КонтекстПриложения = Неопределено Тогда
		ВызватьИсключение "Приложение с таким имененм не зарегистрировано" + ИмяПриложения;
	КонецЕсли;
	
	Возврат КонтекстПриложения;

КонецФункции

Функция КонтекстПриложенияПоИмени(ИмяПриложения)
	Возврат Приложения.Получить(ИмяПриложения);	
КонецФункции

Функция ДобавитьПриложениеИзКаталога(ИмяПриложения, КаталогПриложения) Экспорт

	КонтекстПриложения = ДобавитьКонтекстПриложения(ИмяПриложения);
	
	ЗаполнитьКонтекстИзКаталога(КонтекстПриложения, КаталогПриложения);

	Возврат КонтекстПриложения;

КонецФункции

Процедура ЗаполнитьКонтекстИзКаталога(КонтекстПриложения, КаталогПриложения) Экспорт
	
	ПодключитьСценарииИзКаталога(КаталогПриложения);
	КонтекстПриложения.ПросканироватьКаталог(КаталогПриложения);
	ПроинициализироватьЖелуди(КонтекстПриложения);

КонецПроцедуры

Процедура ПроинициализироватьЖелуди(КонтекстПриложения)

	ОписанияЖелудей = КонтекстПриложения.ПолучитьОпределенияЖелудей();

	Для каждого КиЗ Из ОписанияЖелудей Цикл
		Если НЕ КиЗ.Ключ = "ОбработкаНапильникомПластилинаНаПолях"  
				И НЕ КиЗ.Ключ = "ОбработкаНапильникомФинальныйШтрих"
				И НЕ КиЗ.Ключ = "СборщикМаршрутов" Тогда
			Желудь = КонтекстПриложения.ПолучитьЖелудь(КиЗ.Ключ);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ПодключитьСценарииИзКаталога(КаталогПриложения)
	Файлы = НайтиФайлы(КаталогПриложения, "*.os", Истина);
	Для Каждого Файл Из Файлы Цикл
		ПодключитьСценарий(Файл.ПолноеИмя, Файл.ИмяБезРасширения);	
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьКаталогСтатичныхФайлов(КаталогФайлов, ПутьНаСайте = "/") Экспорт

	ИмяПриложения = "МенеджерФайлов";
	КонтекстПриложения = КонтекстПриложенияПоИмени(ИмяПриложения);	

	Если КонтекстПриложения = Неопределено Тогда
		КонтекстПриложения = ДобавитьКонтекстПриложения(ИмяПриложения);
	КонецЕсли;

	КонтекстПриложения.ЗарегистрироватьЖелудь(Тип("РегистраторКаталогаФайлов"));

	МенеджерФайлов = КонтекстПриложения.ПолучитьЖелудь("РегистраторКаталогаФайлов");

	МенеджерФайлов.ДобавитьКаталогСтатичныхФайлов(КаталогФайлов, ПутьНаСайте);

КонецПроцедуры