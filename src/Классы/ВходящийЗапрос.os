#Использовать json

Перем ТекстЗапроса Экспорт;
Перем Заголовки Экспорт;
Перем Тело Экспорт;
Перем ТелоДвоичныеДанные Экспорт;
Перем Метод Экспорт;
Перем ПолныйПуть Экспорт;
Перем Путь Экспорт;
Перем ПараметрыИменные Экспорт;
Перем ПараметрыПорядковые Экспорт;
Перем ДатаПолучения Экспорт;
Перем ДвоичныеДанные Экспорт;
Перем Сессия Экспорт;
Перем КлючРукопожатия Экспорт;

Перем Куки Экспорт;
Перем Парсеры;
Перем Настройки;


&Желудь
&Характер("Компанейский")
&ОсобоеОбращение(ОтключитьВсеНапильники = Истина)
Процедура ПриСозданииОбъекта(
	&Пластилин("Куки") _Куки,
	&Пластилин("Парсеры") _Парсеры,
	&Пластилин("Настройки") _Настройки
)

	ТекстЗапроса = "";
	Заголовки = Новый Соответствие();
	Тело = "";
	Метод = "";
	ПолныйПуть = "";
	Путь = "";
	ПараметрыИменные = Новый Соответствие();
	ПараметрыПорядковые = Новый Массив();
	ДатаПолучения = ТекущаяДата();

	Куки = _Куки;
	Парсеры = _Парсеры;
	Настройки = _Настройки;

КонецПроцедуры

Функция ЗначенияПараметровДляТочкиМаршрута() Экспорт
	Результат = Новый Структура();
	Результат.Вставить("Запрос", ЭтотОбъект);
	Результат.Вставить("ТекстЗапроса", ТекстЗапроса);
	Результат.Вставить("ЗаголовкиЗапроса", Заголовки);
	Результат.Вставить("ТелоЗапроса", Тело);
	Результат.Вставить("ТелоЗапросаДвоичныеДанные", ТелоДвоичныеДанные);
	Результат.Вставить("МетодЗапроса", Метод);
	Результат.Вставить("ПолныйПутьЗапроса", ПолныйПуть);
	Результат.Вставить("ПутьЗапроса", Путь);
	Результат.Вставить("ПараметрыЗапросаИменные", ПараметрыИменные);
	Результат.Вставить("ПараметрыЗапросаПорядковые", ПараметрыПорядковые);
	Результат.Вставить("ДатаПолученияЗапроса", ДатаПолучения);
	Результат.Вставить("ДвоичныеДанныеЗапроса", ДвоичныеДанные);
	Результат.Вставить("КукиЗапроса", Куки);
	Результат.Вставить("ТелоЗапросОбъект", ТелоЗапросОбъект());
	Результат.Вставить("ДанныеФормы", ДанныеФормы());

	ДополнитьПараметрамиФормы(Результат);

	Возврат Результат;
КонецФункции

Функция ДанныеФормы()
	
	ДанныеФормы = Новый ДанныеСоставнойФормы();

	ЧастьРазделителя = ПолучитьДвоичныеДанныеИзСтроки("--");
	ЧастьРазделителяСВКПС = ПолучитьДвоичныеДанныеИзСтроки("--" + Символы.ВК + Символы.ПС);
	РазделительСтрок = ПолучитьДвоичныеДанныеИзСтроки(Символы.ВК + Символы.ПС);

	Если СтрНайти(НРег(Заголовки["Content-Type"]), "multipart/form-data") > 0 Тогда
		РазделительДанных = СокрЛП(СтрЗаменить(Заголовки["Content-Type"], "multipart/form-data; boundary=", ""));
		РазделительДанныхДД = ПолучитьДвоичныеДанныеИзСтроки(РазделительДанных);

		ДДФормы = Парсеры.РазделитьДвоичныеДанныеРазделителемВМассив(ТелоДвоичныеДанные, РазделительДанныхДД);

		Для Каждого ЧастьФормы из ДДФормы Цикл
			Если ЧастьФормы = ЧастьРазделителяСВКПС 
					ИЛИ ЧастьФормы = ЧастьРазделителя Тогда
				Продолжить;
			КонецЕсли;
			Структура = Новый Структура("Метаданные, Значение", Новый Соответствие);

			РазделеннаяЧастьФормы = Парсеры.РазделитьДвоичныеДанныеРазделителемВМассив(ЧастьФормы, РазделительСтрок);
			
			Если РазделеннаяЧастьФормы[РазделеннаяЧастьФормы.ВГраница()] = ЧастьРазделителя Тогда
				РазделеннаяЧастьФормы.Удалить(РазделеннаяЧастьФормы.ВГраница());
			КонецЕсли;

			Если РазделеннаяЧастьФормы.Количество() = 1 Тогда
				Метаданные = РазделеннаяЧастьФормы[0];
			ИначеЕсли РазделеннаяЧастьФормы.Количество() > 1 Тогда
				Структура.Значение = РазделеннаяЧастьФормы[РазделеннаяЧастьФормы.ВГраница()];
				РазделеннаяЧастьФормы.Удалить(РазделеннаяЧастьФормы.ВГраница());
				МетаданныеДД = Новый Массив();
				Для Каждого РазделеннаяЧасть из РазделеннаяЧастьФормы Цикл
					МетаданныеДД.Добавить(РазделеннаяЧасть);
					МетаданныеДД.Добавить(РазделительСтрок);
				КонецЦикла;
				Метаданные = СоединитьДвоичныеДанные(МетаданныеДД);
			Иначе
				Продолжить;
			КонецЕсли;

			СтрокиМетаданных = СтрРазделить(ПолучитьСтрокуИзДвоичныхДанных(Метаданные), Символы.ПС, Ложь);

			ПлоскиеЗаголовки = Новый Соответствие();

			Для Каждого СтрокаМетаданных из СтрокиМетаданных Цикл
				РазделеннаяСтрока = Парсеры.РазделитьСтроку(СтрокаМетаданных, ":");
				ПлоскиеЗаголовки.Вставить(СокрЛП(РазделеннаяСтрока.Лево), СокрЛП(РазделеннаяСтрока.Право));
				Структура.Метаданные.Вставить(СокрЛП(РазделеннаяСтрока.Лево), 
							УбратьКовычкиИзЗначенийСоответствия(Парсеры.ПараметрыИзТекста(РазделеннаяСтрока.Право, "; ")));	
			КонецЦикла;

			Структура.Метаданные.Вставить("Заголовки", ПлоскиеЗаголовки);

			ДанныеФормы.Добавить(Структура);
			
		КонецЦикла

	КонецЕсли;

	Возврат ДанныеФормы;
КонецФункции

Функция УбратьКовычкиИзЗначенийСоответствия(Соответствие)
	Результат = Новый Соответствие();
	Для Каждого КиЗ Из Соответствие Цикл
		Результат.Вставить(СокрЛП(КиЗ.Ключ), СокрЛП(СтрЗаменить(КиЗ.Значение, """", "")));
	КонецЦикла;
	Возврат Результат;
КонецФункции

Процедура ДополнитьПараметрамиФормы(Результат)
	Если ЗначениеЗаполнено(Тело) 
		И СокрЛП(Заголовки["Content-Type"]) = "application/x-www-form-urlencoded" Тогда

		Параметры = Парсеры.ПараметрыИзТекста(Тело);
		Для Каждого Параметр из Параметры Цикл
			Результат.Вставить(Параметр.Ключ, Параметр. Значение);
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры

Функция ТелоЗапросОбъект() Экспорт
	Результат = Неопределено;

	Если ЗначениеЗаполнено(Тело) 
			И СокрЛП(Заголовки["Content-Type"]) = "application/json" Тогда

			Парсер = Новый ПарсерJSON();
			Результат = Парсер.ПрочитатьJSON(Тело, Истина, Ложь, Настройки.АвтоматическиПриводитьОбъектыКСтруктуре);
				
	КонецЕсли;

	Возврат Результат;
КонецФункции

Функция ЭтоЗапросНаВебСокет() Экспорт
	КлючРукопожатия = Заголовки["Sec-WebSocket-Key"];
	Возврат НЕ КлючРукопожатия = Неопределено И НЕ ПустаяСтрока(КлючРукопожатия);
КонецФункции