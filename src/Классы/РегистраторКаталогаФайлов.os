&Пластилин Перем Перечисления;
&Пластилин Перем ФабрикаОтветов;
&Пластилин Перем Маршрутизатор;

&Деталька("winow.КаталогиСФайлами")
Перем КаталогиСФайлами;

Перем СоответствиеПутей;
Перем ЗарегистрированныеФайлы;

&Желудь
Процедура ПриСозданииОбъекта()
	СоответствиеПутей = Новый Соответствие();
	ЗарегистрированныеФайлы = Новый Массив;
КонецПроцедуры

Процедура ОбновтитьСтатичныеФайлы() Экспорт
	Если ТипЗнч(КаталогиСФайлами) = Тип("Соответствие") Тогда
		Для Каждого КиЗ из КаталогиСФайлами Цикл
			ДобавитьКаталогСтатичныхФайлов(КиЗ.Значение, КиЗ.Ключ);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьКаталогСтатичныхФайлов(КаталогФайлов, ПутьНаСайте = "/") Экспорт
	Для Каждого ТипФайла из Перечисления.ОписанияТиповРасширенийФайлов Цикл 
		Файлы = НайтиФайлы(КаталогФайлов, "*." + ТипФайла.Ключ, Истина);
		Для каждого Файл Из Файлы Цикл
			ДобавитьМаршрутДоФайла(Файл, КаталогФайлов, ПутьНаСайте, ТипФайла.Значение);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура ОтдатьФайл(Запрос, Ответ) Экспорт
	
	ОписаниеФайла = СоответствиеПутей[Запрос.Путь];

	Если ОписаниеФайла = Неопределено Тогда
		Ответ = ФабрикаОтветов.НовыйОтвет404();
	Иначе
		Ответ.Заголовки["Content-Type"] = ОписаниеФайла.ТипКонтента;
		Ответ.ТелоДвоичныеДанные = Новый ДвоичныеДанные(ОписаниеФайла.ПутьНаДиске);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьМаршрутДоФайла(Файл, знач КаталогФайлов, знач ПутьНаСайте, знач ТипКонтента)
	
	Если Не СтрЗаканчиваетсяНа(КаталогФайлов, "/") Тогда
		КаталогФайлов = КаталогФайлов + "/";
	КонецЕсли;

	ПутьЮниксСлешами = СтрЗаменить(Файл.ПолноеИмя, "\", "/");

	Если ЗарегистрированныеФайлы.Найти(ПутьЮниксСлешами) = Неопределено Тогда
		ЗарегистрированныеФайлы.Добавить(ПутьЮниксСлешами);
	Иначе
		Возврат;
	КонецЕсли;

	ПутьВПодкаталоге = ПутьВПодкаталоге(ПутьЮниксСлешами, КаталогФайлов);

	РасположениеНаСайте = СтрЗаменить(ОбъединитьПути(ПутьНаСайте, ПутьВПодкаталоге), "\", "/");

	СоответствиеПутей.Вставить(РасположениеНаСайте, Новый Структура("ПутьНаДиске, ТипКонтента", ПутьЮниксСлешами, ТипКонтента));

	Массив = Новый Массив();
	Массив.Добавить("Запрос");
	Массив.Добавить("Ответ");

	Действие = Маршрутизатор.ОписаниеТочкиМаршрута();
	Действие.Желудь = ЭтотОбъект;
	Действие.ИмяМетода = "ОтдатьФайл";
	Действие.Параметры = Массив;

	Маршрутизатор.ДобавитьМаршрут(РасположениеНаСайте, Действие);

КонецПроцедуры

Функция ПутьВПодкаталоге(знач ПолныйПуть, знач КаталогФайлов)

	Если СтрНачинаетсяС(КаталогФайлов, ".") Тогда
		КаталогФайлов = Сред(КаталогФайлов, 2);
	КонецЕсли;

	Вхождение = СтрНайти(ПолныйПуть, КаталогФайлов, НаправлениеПоиска.СКонца);

	Результат = Сред(ПолныйПуть, Вхождение + СтрДлина(КаталогФайлов));

	Возврат Результат;
	
КонецФункции