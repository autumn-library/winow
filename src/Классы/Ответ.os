Перем Перечисления;
Перем Шаблон;

Перем СостояниеКод Экспорт;
Перем СостояниеТекст Экспорт;
Перем Заголовки Экспорт;
Перем ТелоТекст Экспорт;
Перем ТелоДвоичныеДанные Экспорт;
Перем Модель Экспорт;

&Пластилин Перем Куки Экспорт;
&Пластилин Перем Поделка Экспорт;

&Желудь
&Характер("Компанейский")
Процедура ПриСозданииОбъекта(&Пластилин("Перечисления") _Перечисления)
	Перечисления = _Перечисления;
	СостояниеТекст = "";
	СостояниеКод = "";
	Заголовки = Новый Соответствие();
	ТелоТекст = "";
КонецПроцедуры

Функция ЗначенияПараметровДляТочкиМаршрута() Экспорт
	Результат = Новый Структура();
	Результат.Вставить("Ответ", ЭтотОбъект);

	Возврат Результат;
КонецФункции

Процедура УстановитьТекстШаблона(ТекстШаблона) Экспорт
	Параметры = Новый Массив();
	Параметры.Добавить(ТекстШаблона);
	Шаблон = Поделка.НайтиЖелудь("Шаблон", Параметры);
КонецПроцедуры

Процедура ОбработатьМодель() Экспорт

	Если ЗначениеЗаполнено(Шаблон) и ЗначениеЗаполнено(Шаблон.ТекстШаблона) Тогда
		ТелоТекст = Шаблон.СформироватьТекст(Модель);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьСостояние(КодСостояния) Экспорт
	СостояниеКод = КодСостояния;
	СостояниеТекст = Перечисления.СостоянияОтветов[КодСостояния];	
КонецПроцедуры

Процедура УстановитьСостояниеОК() Экспорт
	УстановитьСостояние(200);
КонецПроцедуры

Процедура УстановитьСостояниеНеАвторизован() Экспорт
	УстановитьСостояние(401);
КонецПроцедуры

Процедура УстановитьСостояниеДоступЗакрыт() Экспорт
	УстановитьСостояние(403);
КонецПроцедуры

Процедура УстановитьСостояниеНеНайдено() Экспорт
	УстановитьСостояние(404);
КонецПроцедуры

Процедура УстановитьСостояниеОшибка() Экспорт
	УстановитьСостояние(500);
КонецПроцедуры

Процедура УстановитьТипКонтента(ТипКонтента) Экспорт
	ОписаниеКонтента = Перечисления.ОписанияТиповРасширенийФайлов[ТипКонтента];
	Если ОписаниеКонтента = Неопределено Тогда
		ВызватьИсключение СтрШаблон("Тип контента %1 не определен", ТипКонтента);
	КонецЕсли;
	
	Заголовки["Content-Type"] = ОписаниеКонтента;
КонецПроцедуры

Процедура Перенаправить(НовыйАдрес) Экспорт
	УстановитьСостояние(302);
	Заголовки["Location"] = НовыйАдрес;
КонецПроцедуры