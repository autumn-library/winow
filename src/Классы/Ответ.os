Перем Перечисления;

Перем СостояниеКод Экспорт;
Перем СостояниеТекс Экспорт;
Перем Заголовки Экспорт;
Перем ТелоТекст Экспорт;
Перем ТелоДвоичныеДанные Экспорт;
Перем Модель Экспорт;

&Пластилин("Куки")
Перем Куки Экспорт;

&Пластилин("Шаблон")
Перем Шаблон Экспорт;

&Желудь
&Характер("Компанейский")
Процедура ПриСозданииОбъекта(&Пластилин("Перечисления") _Перечисления)
	Перечисления = _Перечисления;
	СостояниеТекс = "";
	СостояниеКод = "";
	Заголовки = Новый Соответствие();
	ТелоТекст = "";
КонецПроцедуры

Процедура УстановитьСостояние(КодСостояния) Экспорт
	СостояниеКод = КодСостояния;
	СостояниеТекс = Перечисления.СостоянияОтветов[КодСостояния];	
КонецПроцедуры

Процедура УстановитьСостояниеОК() Экспорт
	УстановитьСостояние(200);
КонецПроцедуры

Процедура УстановитьСостояниеНеАвторизован() Экспорт
	УстановитьСостояние(401);
КонецПроцедуры

Процедура УстановитьСостояниеДоступЗакрыт() Экспорт
	УстановитьСостояние(403);
КонецПроцедуры

Процедура УстановитьСостояниеНеНайдено() Экспорт
	УстановитьСостояние(404);
КонецПроцедуры

Процедура УстановитьСостояниеОшибка() Экспорт
	УстановитьСостояние(500);
КонецПроцедуры

Процедура УстановитьТипКонтента(ТипКонтента) Экспорт
	ОписаниеКонтента = Перечисления.ОписанияТиповРасширенийФайлов[ТипКонтента];
	Если ОписаниеКонтента = Неопределено Тогда
		ВызватьИсключение СтрШаблон("Тип контента %1 не определен", ТипКонтента);
	КонецЕсли;
	
	Заголовки["Content-Type"] = ОписаниеКонтента;
КонецПроцедуры