
Перем _КодСостояния;
Перем _Тело;
Перем _Заголовки;
Перем _Куки;

&Желудь
&Характер("Компанейский")
Процедура ПриСозданииОбъекта()
	_КодСостояния = 200;
	_Тело = ПолучитьБуферДвоичныхДанныхИзСтроки("");
	_Заголовки = Новый Соответствие;
	_Куки = Новый Соответствие();
КонецПроцедуры

Функция Заголовки(Заголовки) Экспорт
	_Заголовки = Заголовки;
	Возврат ЭтотОбъект;
КонецФункции

Функция Кука(Имя, Значение) Экспорт

	_Куки[Имя] = Значение;
	Возврат ЭтотОбъект;
	
КонецФункции

Функция Заголовок(ИмяЗаголовка, Значение) Экспорт
	_Заголовки[ИмяЗаголовка] = Значение;
	Возврат ЭтотОбъект;
КонецФункции

Функция КодСостояния(КодСостояния) Экспорт
	_КодСостояния = КодСостояния;
	Возврат ЭтотОбъект;
КонецФункции

Функция ТелоИзСтроки(Строка) Экспорт
	_Тело = ПолучитьБуферДвоичныхДанныхИзСтроки(Строка);
	Возврат ЭтотОбъект;
КонецФункции

Функция ТелоИзДвоичныхДанных(ДвоичныеДанные) Экспорт
	_Тело = ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(ДвоичныеДанные);
	Возврат ЭтотОбъект;
КонецФункции

Функция ТелоИзФайла(ИмяФайла) Экспорт
	
	ЧтениеДанных = Новый ЧтениеДанных(ИмяФайла);

	Файл = Новый Файл(ИмяФайла);

	БуферДвоичныхДанных = Новый БуферДвоичныхДанных(Файл.Размер());

	ЧтениеДанных.ПрочитатьВБуферДвоичныхДанных(БуферДвоичныхДанных);
	ЧтениеДанных.Закрыть();

	_Тело = БуферДвоичныхДанных;

	Возврат ЭтотОбъект;
КонецФункции

Функция ПолучитьТело() Экспорт
	Возврат _Тело;
КонецФункции

Функция ПолучитьЗаголовки() Экспорт
	Возврат _Заголовки;
КонецФункции

Функция ПолучитьКодСостояния() Экспорт
	Возврат _КодСостояния;
КонецФункции

Функция ПолучитьКуки() Экспорт
	Возврат _Куки;
КонецФункции
