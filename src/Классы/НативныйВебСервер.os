
&Пластилин 
Перем Настройки;

&Пластилин 
Перем МенеджерСессий;

&Пластилин
Перем Поделка;

&Пластилин
Перем ОбработчикЗапросов;

Перем ВебСервер;
Перем ОтдельныйПоток;
Перем Задание;

&Желудь
Процедура ПриСозданииОбъекта()
	ОтдельныйПоток = Ложь;
КонецПроцедуры

Процедура Старт() Экспорт

	ЗапуститьСинхронно();
	
КонецПроцедуры

Процедура ЗапуститьСинхронно() Экспорт
	ВебСервер = Новый ВебСервер(Настройки.Порт);
	ВебСервер.ДобавитьОбработчикЗапросов(ЭтотОбъект, "ОбработатьЗапрос");
	ВебСервер.Запустить();
КонецПроцедуры

Процедура Запустить() Экспорт
	Задание = ФоновыеЗадания.Выполнить(ЭтотОбъект, "ЗапуститьСинхронно", , ОтдельныйПоток);
КонецПроцедуры

Процедура ОжидатьЗавершения() Экспорт
	Задание.ОжидатьЗавершения(0);
КонецПроцедуры

Процедура ОбработатьЗапрос(Контекст, СледующийОбработчик) Экспорт

	Запрос = ЗапросИзКонтекста(Контекст);

	ОтветВино = ОбработчикЗапросов.СформироватьОтвет(Запрос);

	Ответ = ПодготовитьОтвет(ОтветВино);

	Заголовки = Ответ.ПолучитьЗаголовки();
	Куки = Ответ.ПолучитьКуки();

	Для Каждого Элемент Из Заголовки Цикл
		Контекст.Ответ.Заголовки[Элемент.Ключ] = Элемент.Значение;
	КонецЦикла;

	Для Каждого Кука Из Куки Цикл
		Контекст.Ответ.Куки.Добавить(Кука.Ключ, Кука.Значение.Значение);//, Кука.Значение.Параметры);
	КонецЦикла;

	Контекст.Ответ.КодСостояния = Ответ.ПолучитьКодСостояния();

	БуферДвоичныхДанных = Ответ.ПолучитьТело();

	Контекст.Ответ.Тело.Записать(БуферДвоичныхДанных, 0, БуферДвоичныхДанных.Размер);

КонецПроцедуры

Функция ПодготовитьОтвет(ОтветВино)
	Ответ = Новый ХттпОтвет;
	Ответ.КодСостояния(ОтветВино.СостояниеКод)
		 .Заголовки(ОтветВино.Заголовки)
		 .ТелоИзДвоичныхДанных(?(ОтветВино.ТелоДвоичныеДанные = Неопределено, 
		 	ПолучитьДвоичныеДанныеИзСтроки(ОтветВино.ТелоТекст),
			ОтветВино.ТелоДвоичныеДанные));

	Для Каждого Кука Из ОтветВино.Куки.Получить() Цикл
		ПараметрыКуки = Новый Структура("Путь, Истекает, РежимSameSite, ТолькоHttp, МаксимальныйВозраст");
		ПараметрыКуки.Путь = Кука.Значение.Путь;
		ПараметрыКуки.Истекает = Кука.Значение.ДатаИстечения;
		ПараметрыКуки.РежимSameSite = Кука.Значение.ТотжеСайт;
		ПараметрыКуки.ТолькоHttp = Кука.Значение.ТолькоХттп;
		ПараметрыКуки.МаксимальныйВозраст = Кука.Значение.СрокЖизни;
		Ответ.Кука(Кука.Ключ, Кука.Значение.Значение, ПараметрыКуки);
	КонецЦикла;

	Возврат Ответ;
КонецФункции

Функция ЗапросИзКонтекста(Контекст)

	Запрос = Поделка.НайтиЖелудь("ВходящийЗапрос");
	Запрос.Метод = Контекст.Запрос.Метод;
	Для Каждого Заголовок из Контекст.Запрос.Заголовки Цикл
		Запрос.Заголовки.Вставить(Заголовок.Ключ, Заголовок.Значение);
	КонецЦикла;
	Для Каждого Кука Из Контекст.Запрос.Куки Цикл
		Запрос.Куки.Добавить(Кука.Ключ, Кука.Значение);
	КонецЦикла;

	Запрос.Путь = РаскодироватьСтроку(Контекст.Запрос.Путь,СпособКодированияСтроки.КодировкаURL);
	Запрос.ПолныйПуть = Контекст.Запрос.Путь + РаскодироватьСтроку(Контекст.Запрос.СтрокаПараметров,СпособКодированияСтроки.КодировкаURL);

	ЧтениеДанных = Новый ЧтениеДанных(Контекст.Запрос.Тело);
	ДД = ЧтениеДанных.Прочитать().ПолучитьДвоичныеДанные();

	Запрос.ТелоДвоичныеДанные = ДД;
	Запрос.Тело = ПолучитьСтрокуИзДвоичныхДанных(Запрос.ТелоДвоичныеДанные);

	Запрос.Сессия = МенеджерСессий.ПолучитьСессиюПоКукам(Запрос.Куки);

	Для Каждого Параметр Из Контекст.Запрос.Параметры Цикл
		Запрос.ПараметрыИменные.Вставить(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;

	Возврат Запрос;

КонецФункции
