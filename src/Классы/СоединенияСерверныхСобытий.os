#Использовать semaphore

Перем ПулСоединений;
Перем Подписки;
Перем Семафор;

&Желудь
Процедура ПриСозданииОбъекта(&Пластилин ПодпискиСерверныхСобытий)
	Подписки = ПодпискиСерверныхСобытий;
	Инициализация();
КонецПроцедуры

Функция Количество(Топик) Экспорт
	Возврат ПолучитьСоединенияПоТопику(Топик).Количество();
КонецФункции

Функция ПулСоединений() Экспорт
	Возврат ПулСоединений.Скопировать(,"Соединение, ИдСоединения");
КонецФункции

Процедура Добавить(Соединение, Идентификатор, Топик) Экспорт

	Семафор.Захватить();

	НовоеСоединение = ПулСоединений.Добавить();
	НовоеСоединение.Идентификатор = Идентификатор;
	НовоеСоединение.Топик = Топик;
	НовоеСоединение.Соединение = Соединение;
	НовоеСоединение.ИдСоединения = Строка(Новый УникальныйИдентификатор());

	Семафор.Освободить();

	ОповеститьОткрытиеСоединения(Идентификатор, Топик, НовоеСоединение.ИдСоединения);
	
КонецПроцедуры

Процедура Удалить(Идентификатор, Топик) Экспорт
	Содединения = ПолучитьСоединениеПоИдентификатору(Топик, Идентификатор);
	Для Каждого СтрокаСоединения из Содединения Цикл
		УдалитьСтрокуПула(СтрокаСоединения);
	КонецЦикла;
КонецПроцедуры

Процедура УдалитьСтрокуПулаПоИдСоединения(ИдСоединения) Экспорт
	Соединения = ПолучитьСоединениеПоИдСоединения(ИдСоединения);

	Если Соединения.Количество() > 0 Тогда
		УдалитьСтрокуПула(Соединения[0]);
	КонецЕсли;
КонецПроцедуры

Процедура УдалитьСтрокуПула(СтрокаСоединения) Экспорт

	Семафор.Захватить();

	Идентификатор = СтрокаСоединения.Идентификатор;
	Топик = СтрокаСоединения.Топик;
	ИдСоединения = СтрокаСоединения.ИдСоединения;

	ЗакрытьСоединениеВПопытке(СтрокаСоединения.Соединение);
	ОсвободитьОбъект(СтрокаСоединения.Соединение);
	ПулСоединений.Удалить(СтрокаСоединения);

	Семафор.Освободить();

	ОповеститьЗакрытиеСоединения(Идентификатор, Топик, ИдСоединения);
КонецПроцедуры

Функция ПолучитьСоединенияПоТопику(Топик) Экспорт
	Возврат ПолучитьСоединенияСОтбором(Новый Структура("Топик", Топик));
КонецФункции

Функция ПолучитьСоединенияПоТопикуСпискуИдентификаторов(Топик, СписокИдентификаторов) Экспорт
	Соединения = ПолучитьСоединенияПоТопику(Топик);
	Результат = Новый Массив();
	Для Каждого ДанныеСоединения из Соединения Цикл
		Если НЕ СписокИдентификаторов.Найти(ДанныеСоединения.ИдСоединения) = Неопределено Тогда
			Результат.Добавить(ДанныеСоединения);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция ПолучитьСоединенияПоТопикуКромеСпискаИдентификаторов(Топик, СписокИсключенийИдентификаторов) Экспорт
	Соединения = ПолучитьСоединенияПоТопику(Топик);
	Результат = Новый Массив();
	Для Каждого ДанныеСоединения из Соединения Цикл
		Если СписокИсключенийИдентификаторов.Найти(ДанныеСоединения.ИдСоединения) = Неопределено Тогда
			Результат.Добавить(ДанныеСоединения);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция ПолучитьСоединениеПоИдентификатору(Топик, Идентификатор) Экспорт

	Возврат ПолучитьСоединенияСОтбором(Новый Структура("Топик, Идентификатор", Топик, Идентификатор));
	
КонецФункции

Функция ПолучитьСоединениеПоИдСоединения(ИдСоединения) Экспорт

	Возврат ПолучитьСоединенияСОтбором(Новый Структура("ИдСоединения", ИдСоединения));
	
КонецФункции

Функция ПолучитьСоединенияСОтбором(Отбор) Экспорт
	Возврат ПулСоединений.НайтиСтроки(Отбор);
КонецФункции

Процедура Инициализация()
	Семафор = Новый Семафор();
	ПулСоединений = Новый ТаблицаЗначений();
	ПулСоединений.Колонки.Добавить("Идентификатор");
	ПулСоединений.Колонки.Добавить("Топик");
	ПулСоединений.Колонки.Добавить("Соединение");
	ПулСоединений.Колонки.Добавить("ИдСоединения");

	// TODO: В 2.0 не работают индексы. Ждем фикса
	// ПулСоединений.Индексы.Добавить("Топик");
	// ПулСоединений.Индексы.Добавить("ИдСоединения");
	// ПулСоединений.Индексы.Добавить("Идентификатор");
	// ПулСоединений.Индексы.Добавить("Топик, Идентификатор");
	
КонецПроцедуры

Процедура ЗакрытьСоединениеВПопытке(Соединение) Экспорт
	Попытка
		Соединение.Закрыть();	
	Исключение		
	КонецПопытки;
КонецПроцедуры

Процедура ОповеститьЗакрытиеСоединения(Идентификатор, Топик, ИдСоединения)
	Подписки.СобытиеЗакрытия(Топик, Идентификатор, ИдСоединения);
КонецПроцедуры

Процедура ОповеститьОткрытиеСоединения(Идентификатор, Топик, ИдСоединения)
	Подписки.СобытиеОткрытия(Топик, Идентификатор, ИдСоединения);
КонецПроцедуры

