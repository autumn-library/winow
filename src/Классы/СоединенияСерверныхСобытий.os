Перем ПулСоединений;
Перем Подписки;

&Желудь
Процедура ПриСозданииОбъекта(&Пластилин ПодпискиСерверныхСобытий)
	Подписки = ПодпискиСерверныхСобытий;
	Инициализация();
КонецПроцедуры

Функция Количество(Топик) Экспорт
	Возврат ПолучитьСоединенияПоТопику(Топик).Количество();
КонецФункции

Функция ПулСоединений() Экспорт
	Возврат ПулСоединений;
КонецФункции

Процедура Добавить(Соединение, Идентификатор, Топик) Экспорт

	НовоеСоединение = ПулСоединений.Добавить();
	НовоеСоединение.Идентификатор = Идентификатор;
	НовоеСоединение.Топик = Топик;
	НовоеСоединение.Соединение = Соединение;
	НовоеСоединение.ИдСоединения = Строка(Новый УникальныйИдентификатор());

	ОповеститьОткрытиеСоединения(Идентификатор, Топик, НовоеСоединение.ИдСоединения);
	
КонецПроцедуры

Процедура Удалить(Идентификатор, Топик) Экспорт
	Содединения = ПолучитьСоединениеПоИдентификатору(Топик, Идентификатор);
	Для Каждого СтрокаСоединения из Содединения Цикл
		УдалитьСтрокуПула(СтрокаСоединения);
	КонецЦикла;
КонецПроцедуры

Процедура УдалитьСтрокуПула(СтрокаСоединения) Экспорт

	Идентификатор = СтрокаСоединения.Идентификатор;
	Топик = СтрокаСоединения.Топик;
	ИдСоединения = СтрокаСоединения.ИдСоединения;

	ЗакрытьСоединениеВПопытке(СтрокаСоединения.Соединение);
	ОсвободитьОбъект(СтрокаСоединения.Соединение);
	ПулСоединений.Удалить(СтрокаСоединения);

	ОповеститьЗакрытиеСоединения(Идентификатор, Топик, ИдСоединения);
КонецПроцедуры

Функция ПолучитьСоединенияПоТопику(Топик) Экспорт
	Возврат ПолучитьСоединенияСОтбором(Новый Структура("Топик", Топик));
КонецФункции

Функция ПолучитьСоединенияПоТопикуСпискуИдентификаторов(Топик, СписокИдентификаторов) Экспорт
	Соединения = ПолучитьСоединенияПоТопику(Топик);
	Результат = Новый Массив();
	Для Каждого ДанныеСоединения из Соединения Цикл
		Если НЕ СписокИдентификаторов.Найти(ДанныеСоединения.Идентификатор) = Неопределено Тогда
			Результат.Добавить(ДанныеСоединения);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция ПолучитьСоединенияПоТопикуКромеСпискаИдентификаторов(Топик, СписокИсключенийИдентификаторов) Экспорт
	Соединения = ПолучитьСоединенияПоТопику(Топик);
	Результат = Новый Массив();
	Для Каждого ДанныеСоединения из Соединения Цикл
		Если СписокИсключенийИдентификаторов.Найти(ДанныеСоединения.Идентификатор) = Неопределено Тогда
			Результат.Добавить(ДанныеСоединения);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция ПолучитьСоединениеПоИдентификатору(Топик, Идентификатор) Экспорт

	Возврат ПолучитьСоединенияСОтбором(Новый Структура("Топик, Идентификатор", Топик, Идентификатор));
	
КонецФункции

Функция ПолучитьСоединениеПоИдСоединения(ИдСоединения) Экспорт

	Возврат ПолучитьСоединенияСОтбором(Новый Структура("ИдСоединения", ИдСоединения));
	
КонецФункции

Функция ПолучитьСоединенияСОтбором(Отбор) Экспорт
	Возврат ПулСоединений.НайтиСтроки(Отбор);
КонецФункции

Процедура Инициализация()
	ПулСоединений = Новый ТаблицаЗначений();
	ПулСоединений.Колонки.Добавить("Идентификатор");
	ПулСоединений.Колонки.Добавить("Топик");
	ПулСоединений.Колонки.Добавить("Соединение");
	ПулСоединений.Колонки.Добавить("ИдСоединения");
КонецПроцедуры

Процедура ЗакрытьСоединениеВПопытке(Соединение) Экспорт
	Попытка
		Соединение.Закрыть();	
	Исключение		
	КонецПопытки;
КонецПроцедуры

Процедура ОповеститьЗакрытиеСоединения(Идентификатор, Топик, ИдСоединения)
	Подписки.СобытиеЗакрытия(Топик, Идентификатор, ИдСоединения);
КонецПроцедуры

Процедура ОповеститьОткрытиеСоединения(Идентификатор, Топик, ИдСоединения)
	Подписки.СобытиеОткрытия(Топик, Идентификатор, ИдСоединения);
КонецПроцедуры

