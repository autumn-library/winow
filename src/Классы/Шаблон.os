Перем КодВыполнения;
Перем ЧастиШаблона;
Перем РамкиБлоков;
Перем СоответствияРамокБлоков;

Перем ТекстШаблона Экспорт;

&Пластилин Перем Парсеры;
&Пластилин Перем МенеджерОтображений;
&Пластилин Перем Поделка;

&Пластилин(Значение = "ПередОбработкойОтображения", Тип = "Массив") Перем ОбработчикиПеред;
&Пластилин(Значение = "ПослеОбработкиОтображения", Тип = "Массив") Перем ОбработчикиПосле;

&Желудь
&Характер("Компанейский")
Процедура ПриСозданииОбъекта(
							&Блестяшка Текст
							)

	ТекстШаблона = Текст;

	РамкиБлоков = Новый Структура();
	РамкиБлоков.Вставить("НачалоВыражения", 	МассивИзДвухСимволов("{", "{"));
	РамкиБлоков.Вставить("ОкончаниеВыражения", 	МассивИзДвухСимволов("}", "}"));
	РамкиБлоков.Вставить("НачалоОператора", 	МассивИзДвухСимволов("{", "%"));
	РамкиБлоков.Вставить("ОкончаниеОператора", 	МассивИзДвухСимволов("%", "}"));

	СоответствияРамокБлоков = Новый Соответствие;
	СоответствияРамокБлоков.Вставить(СтрСоединить(РамкиБлоков.НачалоВыражения, ""), РамкиБлоков.ОкончаниеВыражения);
	СоответствияРамокБлоков.Вставить(СтрСоединить(РамкиБлоков.НачалоОператора, ""), РамкиБлоков.ОкончаниеОператора);


КонецПроцедуры

Процедура ИнициализацияПеременных()

	КодВыполнения = Новый Массив();
	ЧастиШаблона = Новый Соответствие();
	
КонецПроцедуры

Функция СформироватьТекст(Модель) Экспорт

	ВыполнитьОбработчикиПеред();

	ИнициализацияПеременных();
	
	БлокПоиска = Новый Массив(2);
	ТекущийПоток = Новый Массив;


	ИскомоеОкочание = Неопределено;

	Для Сч = 1 по СтрДлина(ТекстШаблона) Цикл
		ТекущийСимвол = Сред(ТекстШаблона, Сч, 1);
		Парсеры.ДобавитьВМассивСоСмещением(БлокПоиска, ТекущийСимвол);
		
		Если Парсеры.МассивыРавны(БлокПоиска, РамкиБлоков.НачалоВыражения) 
				или Парсеры.МассивыРавны(БлокПоиска, РамкиБлоков.НачалоОператора) Тогда		
			Ид = ДобавитьЧастьШаблона(ТекущийПоток);
			ТекущийПоток = Новый Массив;
			ИскомоеОкочание = ПолучитьЗакрывающийТег(БлокПоиска);
			ЧастьКодаВставкаЧастиШаблона(Ид);

		ИначеЕсли Не ИскомоеОкочание = Неопределено 
					И Парсеры.МассивыРавны(БлокПоиска, ИскомоеОкочание) Тогда
			ЧастьКода = СтрокаИзМассива(ТекущийПоток);
			ТекущийПоток = Новый Массив;
			ДобавитьЧастьКодаОтОкочания(ЧастьКода, ИскомоеОкочание);
			ИскомоеОкочание = Неопределено;	

		Иначе
			ТекущийПоток.Добавить(ТекущийСимвол);
		КонецЕсли;
	КонецЦикла;

	Если ТекущийПоток.Количество() > 0 Тогда
		ТекущийПоток.Добавить(" ");
		Ид = ДобавитьЧастьШаблона(ТекущийПоток);
		ЧастьКодаВставкаЧастиШаблона(Ид);
	КонецЕсли;

	Результат = Новый Массив;

	ЗаполнитьМассивКодом(Результат, Модель);

	ТекстОтображения = СтрСоединить(Результат, "");

	ВыполнитьОбработчикиПосле(ТекстОтображения);

	Возврат ТекстОтображения;

КонецФункции

Процедура ВыполнитьОбработчикиПеред()
	Для Каждого Обработчик Из ОбработчикиПеред Цикл
		Обработчик.Преобразовать(ТекстШаблона);
	КонецЦикла;
КонецПроцедуры

Процедура ВыполнитьОбработчикиПосле(ТекстРезультата)
	Для Каждого Обработчик Из ОбработчикиПосле Цикл
		Обработчик.Преобразовать(ТекстРезультата);
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьМассивКодом(Результат, Модель)

	ВесьКод = СтрСоединить(КодВыполнения);
	Выполнить(ВесьКод);
	
КонецПроцедуры

Процедура ДобавитьЧастьКодаОтОкочания(ЧастьКода, Окончание)

	Если Окончание = РамкиБлоков.ОкончаниеВыражения Тогда
		КодКДобавлению = СтрШаблон("_Значение = Строка(%1);
									|Результат.Добавить(_Значение);", ЧастьКода);
		КодВыполнения.Добавить(КодКДобавлению + Символы.ПС);
	Иначе
		КодВыполнения.Добавить(ЧастьКода + Символы.ПС);
	КонецЕсли;

КонецПроцедуры

Процедура ЧастьКодаВставкаЧастиШаблона(Ид)

	Код = СтрШаблон("Результат.Добавить(ЧастиШаблона[""%1""]);", Ид);
	КодВыполнения.Добавить(Код + Символы.ПС);
	
КонецПроцедуры

Функция ДобавитьЧастьШаблона(МассивСимволов)

	Строка = СтрокаИзМассива(МассивСимволов);
	Ид = ХешироватьСтроку(Строка);
	
	ЧастиШаблона.Вставить(Ид, Строка);

	Возврат Ид;
	
КонецФункции

Функция СтрокаИзМассива(МассивСимволов)
	МассивСимволов.Удалить(МассивСимволов.ВГраница());
	Строка = СтрСоединить(МассивСимволов, "");
	Возврат Строка; 	
КонецФункции

Функция ХешироватьСтроку(Строка)  
	
	Хеширование = Новый ХешированиеДанных(ХешФункция.MD5);
	
	Хеширование.Добавить(Строка);
	
	Возврат Base64Строка(Хеширование.ХешСумма);
	
КонецФункции

Функция МассивИзДвухСимволов(Символ1, Символ2)

	Массив = Новый Массив(2);
	Массив[0] = Символ1;
	Массив[1] = Символ2;
	
	Возврат Массив;

КонецФункции

Функция ПолучитьЗакрывающийТег(ОткрывающийТег)

	Возврат СоответствияРамокБлоков.Получить(СтрСоединить(ОткрывающийТег,""));	

КонецФункции

Функция ВывестиПоШаблону(_ПутьДоШаблона, _Модель)

	ВложенныйШаблон = НовыйЭкземплярШаблона(_ПутьДоШаблона); 
	
	Возврат ВложенныйШаблон.СформироватьТекст(_Модель);

КонецФункции

Функция НовыйЭкземплярШаблона(_ПутьДоШаблона)

	Параметры = Новый Массив();
	Параметры.Добавить(МенеджерОтображений.ПолучитьОтображениеИзФайла(_ПутьДоШаблона));

	Возврат Поделка.НайтиЖелудь("Шаблон", Параметры);

КонецФункции 