
Перем ИнтервалПроверки;
Перем Сообщение;
Перем Проверять;

&Пластилин
Перем СоединенияСерверныхСобытий;
&Пластилин
Перем БрокерСообщенийСобытийСервера;

&Желудь
Процедура ПриСозданииОбъекта(&Пластилин Настройки, &Пластилин ФабрикаОтветов)
	ИнтервалПроверки = Настройки.ИнтервалПроверкиСоединенийСерверныхСобытий;

	Сообщение = ПолучитьДвоичныеДанныеИзСтроки(ФабрикаОтветов.СерверноеСобытие()
				.ТипСобытия("ping")
				.ДобавитьСтроку("")
				.Сформировать());

КонецПроцедуры

Процедура Старт() Экспорт
	
	Если Проверять = Истина Тогда
		Возврат;
	КонецЕсли;

	Проверять = Истина;
	ФоновыеЗадания.Выполнить(ЭтотОбъект, "ПроверятьВЦикле");
КонецПроцедуры

Процедура Стоп() Экспорт
	Проверять = Ложь;
КонецПроцедуры

Процедура ПроверятьВЦикле() Экспорт

	Пока Проверять Цикл
		Приостановить(ИнтервалПроверки);
		Попытка
			НайтиИУдалитьСоединения();
		Исключение
			Сообщить("Что-то пошло нетак в проверке соединений SSE " + ОписаниеОшибки());
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура НайтиИУдалитьСоединения()

	ПулСоеднинений = СоединенияСерверныхСобытий.ПулСоединений();
	ИдСоединенийКУдалению = Новый Массив();
	Для Каждого СтрокаСоединения из ПулСоеднинений Цикл
		Если НЕ СоединениеАктивно(СтрокаСоединения.Соединение) Тогда
			ИдСоединенийКУдалению.Добавить(СтрокаСоединения.ИдСоединения);
		КонецЕсли;
	КонецЦикла;

	Для Каждого ИдСоединения из ИдСоединенийКУдалению Цикл
		СоединенияСерверныхСобытий.УдалитьСтрокуПулаПоИдСоединения(ИдСоединения);
	КонецЦикла;

КонецПроцедуры

Функция СоединениеАктивно(Соединение)
	Возврат БрокерСообщенийСобытийСервера.ОтправитьВПопытке(Соединение, Сообщение);
КонецФункции