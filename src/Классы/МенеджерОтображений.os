Перем ОтображенияДействий;
Перем Отображения;

&Желудь
Процедура ПриСозданииОбъекта()

	ОтображенияДействий = Новый Соответствие();
	Отображения = Новый Соответствие();
	УстановитьСтандартныеШаблоны();	

КонецПроцедуры

Процедура ДобавитьОтображениеДействия(Действие, ПутьДоФайла) Экспорт

	ДобавитьОтображениеИзФайла(ПутьДоФайла);
	ОтображенияДействий.Вставить(Действие, ПутьДоФайла);

КонецПроцедуры

Процедура УстановитьСтандартныеШаблоны()
	УстановитьШаблон401(СтандартныйШаблон());
	УстановитьШаблон403(СтандартныйШаблон());
	УстановитьШаблон404(СтандартныйШаблон());
	УстановитьШаблон500(СтандартныйШаблон());	
КонецПроцедуры

Процедура УстановитьШаблон401(Текст) Экспорт
	УстановитьОтображение(401, Текст);	
КонецПроцедуры

Процедура УстановитьШаблон403(Текст) Экспорт
	УстановитьОтображение(403, Текст);	
КонецПроцедуры

Процедура УстановитьШаблон404(Текст) Экспорт
	УстановитьОтображение(404, Текст);	
КонецПроцедуры

Процедура УстановитьШаблон500(Текст) Экспорт
	УстановитьОтображение(500, Текст);	
КонецПроцедуры

Функция СтандартныйШаблон()
	Возврат "<!DOCTYPE html>
			|<div><h1> {{ Модель.КодСостояния }} </h1></div>
			|<div> {{ Модель.ТекстСообщения }} </div>";
КонецФункции

Функция ДобавитьОтображениеИзФайла(ПутьДоФайла) Экспорт

	Текст = ПолучитьОтображениеИзФайла(ПутьДоФайла);
	УстановитьОтображение(ПутьДоФайла, Текст);
	
	Возврат Текст;

КонецФункции

Процедура УстановитьОтображение(ПутьДоФайла, Текст)
	Отображения.Вставить(ПутьДоФайла, Текст);	
КонецПроцедуры

Функция ПолучитьОтображениеДействия(Действие) Экспорт
	
	ПутьДоФайла = ОтображенияДействий.Получить(Действие);

	Если не ЗначениеЗаполнено(ПутьДоФайла) Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат Отображения[ПутьДоФайла];

КонецФункции

Функция ПолучитьОтображение(ПутьДоФайла) Экспорт

	Отображение = Отображения[ПутьДоФайла];

	Если Отображение = Неопределено Тогда
		Отображение = ДобавитьОтображениеИзФайла(ПутьДоФайла);
	КонецЕсли;

	Возврат Отображение;

КонецФункции

Функция ПолучитьОтображениеИзФайла(ПутьДоФайла) Экспорт

	Файл = Новый Файл(ПутьДоФайла);	
	Если Файл.Существует() Тогда
		Чтение = Новый ЧтениеТекста();
		Чтение.Открыть(ПутьДоФайла, КодировкаТекста.UTF8);
		Текст = Чтение.Прочитать();
		Чтение.Закрыть();

		Возврат Текст;
	Иначе
		ВызватьИсключение СтрШаблон("Файл отображения не существует %1", ПутьДоФайла);
	КонецЕсли;	

КонецФункции