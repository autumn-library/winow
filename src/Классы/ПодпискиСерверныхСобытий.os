
Перем ПодписчикиОткрытия;
Перем ПодписчикиЗакрытия;

&Желудь
Процедура ПриСозданииОбъекта()
	Инициализация();
КонецПроцедуры

Процедура СобытиеОткрытия(Топик, Идентификатор, ИдСоединения) Экспорт
	Для Каждого Подписчики из ПодписчикиОткрытия.НайтиСтроки(Новый Структура("Топик", Топик)) Цикл
		ВыполнитьОбработчикФоном(Подписчики.Обработчик, Идентификатор, ИдСоединения);
	КонецЦикла;
КонецПроцедуры

Процедура СобытиеЗакрытия(Топик, Идентификатор, ИдСоединения) Экспорт
	Для Каждого Подписчики из ПодписчикиЗакрытия.НайтиСтроки(Новый Структура("Топик", Топик)) Цикл
		ВыполнитьОбработчикФоном(Подписчики.Обработчик, Идентификатор, ИдСоединения);
	КонецЦикла;
КонецПроцедуры

Процедура ВыполнитьОбработчикФоном(Обработчик, Идентификатор, ИдСоединения)
	Параметры = Новый Массив();
	Параметры.Добавить(Обработчик);
	Параметры.Добавить(Идентификатор);
	Параметры.Добавить(ИдСоединения);
	ФоновыеЗадания.Выполнить(ЭтотОбъект, "ЗапуститьФоновоеЗаданиеОбработчика", Параметры);
КонецПроцедуры

Процедура ЗапуститьФоновоеЗаданиеОбработчика(Обработчик, Идентификатор, ИдСоединения) Экспорт
	Приостановить(50);
	Обработчик.Выполнить(Идентификатор, ИдСоединения);
КонецПроцедуры

Процедура ПодпискаНаОткрытие(Топик, Обработчик) Экспорт
	НоваяПодписка = ПодписчикиОткрытия.Добавить();
	НоваяПодписка.Топик = Топик;
	НоваяПодписка.Обработчик = Обработчик;	
КонецПроцедуры

Процедура ПодпискаНаЗакрытие(Топик, Обработчик) Экспорт
	НоваяПодписка = ПодписчикиЗакрытия.Добавить();
	НоваяПодписка.Топик = Топик;
	НоваяПодписка.Обработчик = Обработчик;	
КонецПроцедуры

Процедура Инициализация()
	ПодписчикиОткрытия = Новый ТаблицаЗначений();
	ПодписчикиОткрытия.Колонки.Добавить("Топик");
	ПодписчикиОткрытия.Колонки.Добавить("Обработчик");

	ПодписчикиЗакрытия = Новый ТаблицаЗначений();
	ПодписчикиЗакрытия.Колонки.Добавить("Топик");
	ПодписчикиЗакрытия.Колонки.Добавить("Обработчик");
КонецПроцедуры

