#Использовать autumn
#Использовать autumn-logos

&Лог("winow.router")
Перем Лог;

&Пластилин Перем Маршрутизатор;
&Пластилин Перем МенеджерОтображений;
&Пластилин Перем МенеджерДоступа;
&Пластилин Перем Перечисления;
&Пластилин Перем Поделка;

&Желудь
Процедура ПриСозданииОбъекта()

КонецПроцедуры

Процедура НайтиИЗарегистрироватьМаршруты(Желудь) Экспорт

	РефлекторОбъекта = РефлекторЖелудя(Желудь);

	МетодыМаршрут = РефлекторОбъекта.ПолучитьТаблицуМетодов("Контроллер", Ложь);

	Если МетодыМаршрут.Количество() > 1 Тогда

		ВызватьИсключение "Не может быть больше одного определения контроллера в классе " + Желудь;

	ИначеЕсли МетодыМаршрут.Количество() = 0 Тогда

		Возврат;

	КонецЕсли;

	Аннотация = РаботаСАннотациями.ПолучитьАннотацию(МетодыМаршрут[0], "Контроллер");

	АдресКонтроллера = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация);

	МетодыТочекМаршрутов = РефлекторОбъекта.ПолучитьТаблицуМетодов("ТочкаМаршрута");

	Если МетодыТочекМаршрутов.Количество() = 0 Тогда
		ВызватьИсключение "Не определено ниодной точки маршрута в контроллере " + Желудь;
	КонецЕсли;

	Лог.Отладка(СтрШаблон("Регистрация контрола %1 по адресу %2", Желудь, АдресКонтроллера));

	Для Каждого ТочкаМаршрута из МетодыТочекМаршрутов Цикл
		ЗарегистрироватьМаршрутКОбъекту(АдресКонтроллера, ТочкаМаршрута, Желудь);
	КонецЦикла;

	НайтиПодпискиВебСокета(Желудь, РефлекторОбъекта);

КонецПроцедуры

Процедура НайтиПодпискиВебСокета(Желудь, РефлекторОбъекта)

	Для каждого ВидПодписки Из Перечисления.ВидыПодписокВебСокета Цикл
		ЗаполнитьПодпискиЖелудя(Желудь, РефлекторОбъекта, ВидПодписки.Ключ);
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьПодпискиЖелудя(Желудь, РефлекторОбъекта, Подписка)
	МетодыПодписки = РефлекторОбъекта.ПолучитьТаблицуМетодов(Подписка);
	Для Каждого Метод из МетодыПодписки Цикл
		Аннотация = РаботаСАннотациями.ПолучитьАннотацию(Метод, Подписка);
		Топик =  РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация);
		Если НЕ ЗначениеЗаполнено(Топик) Тогда
			ВызватьИсключение СтрШаблон("Для контроллера %1 не заполнено топик в подписке %2", Желудь, Метод.Имя);
		КонецЕсли;
		
		Маршрутизатор.ДобавитьПодписку(Подписка, Топик, Желудь, Метод.Имя, Метод.Параметры.ВыгрузитьКолонку("Имя"));
		
	КонецЦикла;
КонецПроцедуры

Процедура ЗарегистрироватьОтображениеКонтрола(Действие)

	РефлекторОбъекта = РефлекторЖелудя(Действие.Желудь);
	КонструкторЖелудя = РефлекторОбъекта.ПолучитьТаблицуМетодов("Контроллер", Ложь)[0];	
	Аннотация = РаботаСАннотациями.ПолучитьАннотацию(КонструкторЖелудя, "Отображение");
	Если Аннотация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Аннотация.Параметры.Количество() = 2 Тогда
		ПутьДоШаблона = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация, "Шаблон");
		Метод = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация, "Метод");
		ПараметрыШаблона = НайтиПараметрыМетодаВТаблице(РефлекторОбъекта.ПолучитьТаблицуМетодов(), Метод);	
	Иначе
		ПутьДоШаблона = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация, "Шаблон");
		Метод = Неопределено;
		ПараметрыШаблона = Новый Массив();
	КонецЕсли;

	Действие.Шаблон = ПутьДоШаблона;
	Действие.МетодШаблона = Метод;
	Действие.ПараметрыШаблона = ПараметрыШаблона;

КонецПроцедуры

Функция НайтиПараметрыМетодаВТаблице(ТаблицаМетодов, ИмяМетода)
	Для Каждого ТекМетод Из ТаблицаМетодов Цикл
		Если ВРег(ИмяМетода) = ВРег(ТекМетод.Имя) Тогда
			Возврат ТекМетод.Параметры.ВыгрузитьКолонку("Имя");
		КонецЕсли;
	КонецЦикла;
	ВызватьИсключение "Метод не найден " + ИмяМетода;
КонецФункции

Функция НайтиИЗаменитьПараметрыУРЛ(Урл)
	Урл = СтрЗаменить(Урл, "\", "/");
	МассивВсехПараметров = СтрРазделить(Урл, "/");
	ОчищенныйУРЛ = Новый Массив;
	ПараметрыУРЛ = Новый Соответствие;

	СчетчикПараметров = 0;

	Для каждого ТекущийПараметр Из МассивВсехПараметров Цикл

		Если ЭтоШаблонПараметраУрл(ТекущийПараметр) Тогда
			ПараметрыУРЛ.Вставить(СчетчикПараметров, Сред(ТекущийПараметр,2, СтрДлина(ТекущийПараметр) - 2));	
			ОчищенныйУРЛ.Добавить(Маршрутизатор.СимволПараметраТочкиМаршрута());
		Иначе
			ОчищенныйУРЛ.Добавить(ТекущийПараметр);
		КонецЕсли;
		СчетчикПараметров = СчетчикПараметров + 1;
	КонецЦикла;
	Урл = СтрСоединить(ОчищенныйУРЛ, "/");
	Возврат ПараметрыУРЛ;
КонецФункции

Функция ЭтоШаблонПараметраУрл(ЧастьУрла)
	Возврат СтрНачинаетсяС(ЧастьУрла, "{") И СтрЗаканчиваетсяНа(ЧастьУрла, "}");
КонецФункции

Процедура ЗарегистрироватьМаршрутКОбъекту(АдресКонтроллера, ТочкаМаршрута, Желудь)

	Действие = Маршрутизатор.ОписаниеТочкиМаршрута();
	Действие.АдресКонтроллера = АдресКонтроллера;
	Действие.Желудь = Желудь;
	Действие.ИмяМетода = ТочкаМаршрута.Имя;
	Действие.Параметры = ТочкаМаршрута.Параметры.ВыгрузитьКолонку("Имя");

	Аннотация = РаботаСАннотациями.ПолучитьАннотацию(ТочкаМаршрута, "ТочкаМаршрута");
	ИмяТочкиМаршута = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация);

	Если СтрНайти(ИмяТочкиМаршута, "{") > 0 
			ИЛИ СтрНайти(ИмяТочкиМаршута, "}") > 0 Тогда
			Действие.ПараметрыУРЛ = НайтиИЗаменитьПараметрыУРЛ(ИмяТочкиМаршута);
			Действие.УрлСодержитШаблон = Истина;
	КонецЕсли;

	ПутьКМетоду = СтрЗаменить(ОбъединитьПути(АдресКонтроллера, ИмяТочкиМаршута), "\", "/");
	
	Лог.Отладка(СтрШаблон("Регистрация точки маршрута контрола %1 по адресу %2", Желудь, ПутьКМетоду));
		
	Маршрутизатор.ДобавитьМаршрут(ПутьКМетоду, Действие);

	ЗарегистрироватьОтображение(ТочкаМаршрута, Действие);

	ЗарегистрироватьРолиТочкиМаршрута(ТочкаМаршрута, Действие);

	ЗарегистрироватьОтображениеКонтрола(Действие);

КонецПроцедуры

Процедура ЗарегистрироватьОтображение(ТочкаМаршрута, Действие)
	Аннотация = РаботаСАннотациями.ПолучитьАннотацию(ТочкаМаршрута, "Отображение");
	Если НЕ Аннотация = Неопределено Тогда
		РасположениеОтображения = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация);
		МенеджерОтображений.ДобавитьОтображениеДействия(Действие, РасположениеОтображения);
	КонецЕсли;	
КонецПроцедуры

Процедура ЗарегистрироватьРолиТочкиМаршрута(ТочкаМаршрута, Действие)
	Аннотация = РаботаСАннотациями.ПолучитьАннотацию(ТочкаМаршрута, "Роли");
	Если НЕ Аннотация = Неопределено Тогда
		СписокРолей = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация);
		МенеджерДоступа.ДобавитьРолиТочкиДоступаСписком(Действие, СписокРолей);
	КонецЕсли;	
КонецПроцедуры

Функция РефлекторЖелудя(Желудь)
	Возврат Новый РефлекторОбъекта(ТипЗнч(Желудь));
КонецФункции