Перем _БрокерСообщенийВебСокетов;
Перем _СоединенияВебСокетов;

Перем _Идентификатор;
Перем _Соединение;
Перем _Топик;
Перем _Включена;

&Желудь
&Характер("Компанейский")
Процедура ПриСозданииОбъекта(&Пластилин БрокерСообщенийВебСокетов,
							&Пластилин СоединенияВебСокетов)
	_СоединенияВебСокетов = СоединенияВебСокетов;
	_БрокерСообщенийВебСокетов = БрокерСообщенийВебСокетов;
	_Включена = Ложь;
КонецПроцедуры

Процедура Подписатся(Соединение, Идентификатор, Топик) Экспорт
	_Идентификатор = Идентификатор;
	_Соединение = Соединение;
	_Топик = Топик;
	_Включена = Истина;

	ФоновыеЗадания.Выполнить(ЭтотОбъект, "ЗапуститьОбработкуСообщений");
КонецПроцедуры

Процедура Отписаться() Экспорт
	_Включена = Ложь;
КонецПроцедуры

Процедура ЗапуститьОбработкуСообщений() Экспорт
	Если НЕ _Соединение = Неопределено Тогда
		_БрокерСообщенийВебСокетов.ПриПодключении(_Топик, _Идентификатор);
		ОбработкаСообщений();
	Иначе
		ВызватьИсключение "Не инициализировано соединение, невозможно запустить обработку сообщений"
	КонецЕсли;	
КонецПроцедуры

Процедура ОбработкаСообщений()
	Попытка
		Пока _Включена = Истина И _Соединение.Активно = Истина Цикл		
			ДвоичныеДанные = _Соединение.ПрочитатьДвоичныеДанные();
			_БрокерСообщенийВебСокетов.ВходящееСообщение(_Идентификатор, _Топик, ДвоичныеДанные);
		КонецЦикла;
	Исключение
	КонецПопытки;
	Отключить();
КонецПроцедуры

Процедура Отключить()
	_СоединенияВебСокетов.УдалитьСоединение(_Соединение);
	_БрокерСообщенийВебСокетов.ПриОтключении(_Топик, _Идентификатор);
КонецПроцедуры