#Использовать logos

Перем Сообщения;
Перем Соединение;
Перем ПервичныйОтвет;
Перем Слушать;
Перем Лог;

Процедура ПриСозданииОбъекта(Хост, Порт, Топик)
	Лог = Логирование.ПолучитьЛог("oscript.app.winow.test.sseclient");
	Сообщения = Новый Массив();
	Соединение = Новый TCPСоединение(Хост, Порт);
	УстановитьПодключение(Топик);
	Слушать = Истина;
	НачатьПрослушку();
КонецПроцедуры

Процедура Остановить() Экспорт
	Слушать = Ложь;
КонецПроцедуры

Процедура ЖдатьСообщения() Экспорт
	Пока Слушать Цикл
		Ответ = Соединение.ПрочитатьСтроку();
		Если НЕ ПустаяСтрока(Ответ) Тогда
			Если СтрНайти(Ответ, "ping") = 0 Тогда
				Сообщения.Добавить(СокрЛП(Ответ));
				Лог.Информация("Принято сообщение: " + Ответ);
			КонецЕсли;
		КонецЕсли;
		Приостановить(100);
	КонецЦикла;
	Соединение.Закрыть();
	Соединение = Неопределено;
	Лог.Информация("Соединение закрыто");
КонецПроцедуры

Процедура НачатьПрослушку()
	ФоновыеЗадания.Выполнить(ЭтотОбъект, "ЖдатьСообщения");
КонецПроцедуры

Функция ПервичныйОтвет() Экспорт
	Возврат ПервичныйОтвет;	
КонецФункции

Функция Сообщения() Экспорт
	Возврат Сообщения;
КонецФункции

Процедура УстановитьПодключение(Топик)
	Лог.Информация("Установка соединения");
	ТекстПодключения = ШаблонПодключения();

	Соединение.ОтправитьСтроку(СтрШаблон(ТекстПодключения, Топик));

	ПервичныйОтвет = Соединение.ПрочитатьСтроку();

	Если Не СтрНачинаетсяС(ПервичныйОтвет, "HTTP/1.1 200 OK") Тогда
		ВызватьИсключение "Не удалось подключиться к топику " + Топик + " по причине: " + ПервичныйОтвет;
	КонецЕсли;
	Лог.Информация("Соединение установлено");
КонецПроцедуры

Функция ШаблонПодключения()

	Текст = 
	"GET %1 HTTP/1.1
	|Accept: text/event-stream
	|Connection: keep-alive
	|";

	Возврат Текст;
	
КонецФункции
